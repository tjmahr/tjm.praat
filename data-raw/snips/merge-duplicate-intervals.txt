form Merge duplicated interval labels
  sentence Textgrid_in
  sentence Target_tier silence
  sentence Textgrid_out
endform

Read from file: textgrid_in$
@findNumberForTier: target_tier$

@labelMerger: "initialize", findNumberForTier.result

while labelMerger.has_next
	@labelMerger: "merge-or-step", findNumberForTier.result
endwhile

Save as text file: textgrid_out$


# Object for merging duplicated textgrid interval labels
procedure labelMerger: .method$, .tier_number

  if .method$ = "initialize"
      .current_position = 1
  endif

  if .method$ = "merge-or-step"
    .can_merge = .current_label$ == .next_label$
    if .can_merge == 1
      Remove right boundary: .tier_number, .current_position
      Set interval text: .tier_number, .current_position, .current_label$
    else
      .current_position = .current_position + 1
    endif
  endif


  if .method$ = "has_next"
    # .has_next is updated whenever procedure is invoked
  endif

  .intervals = Get number of intervals: .tier_number
  .current_label$ = Get label of interval: .tier_number, .current_position

  if .current_position < .intervals
    .has_next = 1
    .next_label$ = Get label of interval: .tier_number, .current_position + 1
  else
    .has_next = 0
  endif

endproc

{findNumberForTier}
